dnl Process this file with autoconf to produce a configure script.
AC_REVISION([$Id: configure.in,v 1.5 2003/10/01 23:36:42 bitman Exp $])
AC_INIT(KevEdit, 0.5.1, kev@kvance.com, kevedit)
AC_CONFIG_SRCDIR(kevedit.c)

dnl Detect host and target build environment
AC_CANONICAL_HOST
AC_CANONICAL_TARGET

AM_INIT_AUTOMAKE

dnl Checks for programs.
AC_PROG_CC
AC_PROG_INSTALL
AC_PROG_MAKE_SET
AC_PROG_RANLIB

dnl Check for windows resource compiler.
AC_CHECK_PROGS(WINDRES, windres)
AC_SUBST(WINDRES)
AM_CONDITIONAL(HAVE_WINDRES, test x$WINDRES != x)

dnl Checks for header files.
AC_HEADER_STDC
AC_HEADER_DIRENT
AC_CHECK_HEADERS(malloc.h strings.h unistd.h)

dnl Checks for typedefs, structures, and compiler characteristics.
AC_C_CONST
AC_TYPE_SIZE_T
AC_HEADER_TIME
AC_CHECK_TYPES(u_int8_t)

dnl Checks for library functions.
AC_FUNC_ALLOCA
AC_FUNC_FNMATCH
AC_TYPE_SIGNAL
AC_FUNC_STRCOLL
AC_CHECK_FUNCS(getcwd strdup strstr)

dnl Check for availability of long file names
AC_SYS_LONG_FILE_NAMES

dnl Check for windows (for glob's sake)
AC_MSG_CHECKING(for Windows)
case "$host" in
	*-*-*cygwin*)
		AC_DEFINE(WINDOWS32)
		AC_MSG_RESULT(yes)
		;;
	*)
		AC_MSG_RESULT(no)
		;;
esac

dnl Define some random things for the sake of the old build system
AC_DEFINE(CANGLOB)
AC_DEFINE_UNQUOTED(keveditVERSION, "$PACKAGE_VERSION")
AC_DEFINE_UNQUOTED(DATAPATH, ".")

dnl Determine available displays
AC_MSG_NOTICE(Searching for available displays)

dnl Check for SDL
AM_PATH_SDL(1.2.0,
	AC_MSG_NOTICE(Using SDL display and sound)
	AC_DEFINE(SDL)
	sdl=true,

	AC_MSG_NOTICE(SDL not available for display))

CFLAGS="$CFLAGS $SDL_CFLAGS"
LIBS="$LIBS $SDL_LIBS"

AM_CONDITIONAL(SDL, test x$sdl = xtrue)

dnl Check for DOS display
case "$host" in
	*-*-msdos*)
		AC_MSG_NOTICE(Using DOS display and pcspeaker sound)
		AC_DEFINE(DOS)
		dos=true
		;;
	*)
		AC_MSG_NOTICE(DOS display not available)
		;;
esac

AM_CONDITIONAL(DOS, test x$dos = xtrue)

AC_PROG_GNU_GLOB

AC_OUTPUT(glob/Makefile libzzt2/Makefile synth/Makefile Makefile)

