dnl Process this file with autoconf to produce a configure script.
AC_REVISION([$Id: configure.in,v 1.8 2004/02/01 06:30:39 bitman Exp $])
AC_INIT(KevEdit, 0.5.1, bitman@users.sourceforge.net, kevedit)
AC_CONFIG_SRCDIR(src/kevedit/kevedit.c)

dnl Detect host and target build environment
AC_CANONICAL_HOST
AC_CANONICAL_TARGET

AM_INIT_AUTOMAKE

dnl Checks for programs.
AC_PROG_CC
AC_PROG_INSTALL
AC_PROG_MAKE_SET
AC_PROG_RANLIB

dnl Determine available displays
AC_MSG_NOTICE(Searching for available displays)

dnl Check for SDL
AM_PATH_SDL(1.2.0,
	AC_MSG_NOTICE(Using SDL display and sound)
	AC_DEFINE(SDL)
	sdl=true,

	AC_MSG_NOTICE(SDL not available for display))

dnl This must be done before most checks because it can affect what headers
dnl and libraries are available
CFLAGS="$CFLAGS $SDL_CFLAGS"
LIBS="$LIBS $SDL_LIBS"

AM_CONDITIONAL(SDL, test x$sdl = xtrue)

dnl Check for DOS display
case "$host" in
	*-*-msdos*)
		AC_MSG_NOTICE(Using DOS display and pcspeaker sound)
		AC_DEFINE(DOS)
		dos=true
		;;
	*)
		AC_MSG_NOTICE(DOS display not available)
		;;
esac

AM_CONDITIONAL(DOS, test x$dos = xtrue)


dnl Check for windows resource compiler.
AC_CHECK_PROGS(WINDRES, windres)
AC_SUBST(WINDRES)
AM_CONDITIONAL(HAVE_WINDRES, test x$WINDRES != x)

dnl Checks for header files.
AC_HEADER_STDC
AC_HEADER_DIRENT
dnl AC_CHECK_HEADERS(strings.h unistd.h)

dnl Checks for typedefs, structures, and compiler characteristics.
dnl AC_C_CONST
dnl AC_TYPE_SIZE_T
dnl AC_HEADER_TIME
AC_CHECK_TYPES(u_int8_t)

dnl Checks for library functions.
dnl AC_FUNC_ALLOCA
dnl AC_FUNC_FNMATCH
AC_TYPE_SIGNAL
dnl AC_FUNC_STRCOLL
AC_CHECK_FUNCS(getcwd strdup strstr)

dnl Check for math library
AC_CHECK_LIB(m, floor)

dnl Check for availability of long file names
AC_SYS_LONG_FILE_NAMES

dnl Check for windows (for glob's sake)
AC_MSG_CHECKING(for Windows)
case "$host" in
	*-*-*cygwin*)
		AC_DEFINE(WINDOWS32)
		AC_MSG_RESULT(yes: cygwin)
		;;
	*-*-*mingw32*)
		AC_DEFINE(WINDOWS32)
		AC_MSG_RESULT(yes: MingW)
		;;
	*)
		AC_MSG_RESULT(no)
		;;
esac

dnl Define some random things for the sake of the old build system
AC_DEFINE(CANGLOB)
AC_DEFINE_UNQUOTED(keveditVERSION, "$PACKAGE_VERSION")
AC_DEFINE_UNQUOTED(DATAPATH, ".")

dnl Check for glob and add it to CPPFLAGS
AC_PROG_GNU_GLOB
CPPFLAGS="$CPPFLAGS $GLOBINC"

dnl Add some useful include paths
AC_SUBST(INCLUDE_PATHS) INCLUDE_PATHS='-I$(top_srcdir)/src -I$(top_builddir)'
CPPFLAGS="$CPPFLAGS $INCLUDE_PATHS"

AC_OUTPUT(docs/Makefile glob/Makefile src/libzzt2/Makefile src/structures/Makefile src/display/Makefile src/synth/Makefile src/themes/Makefile src/themes/rusty/Makefile src/texteditor/Makefile src/help/Makefile src/dialogs/Makefile src/zlaunch/Makefile src/kevedit/Makefile src/Makefile Makefile buildinfo.h)

